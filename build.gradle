plugins {
    id 'java'
    id 'maven-publish'
    id 'io.papermc.paperweight.userdev' version '+' // Enable Development of using Mojang Mapping.
    id 'com.github.johnrengelman.shadow' version '7.1.0' // fatJar
    id 'com.palantir.git-version' version '+' // Git Information Getter
    id 'org.cadixdev.licenser' version '0.6.1' // Add License Header to source code headers.
}

group 'net.unknown'
version versionDetails().gitHash[0..7]

compileJava.options.encoding = 'UTF-8'

compileJava.dependsOn = ["licenseFormat"]
tasks.build.dependsOn(reobfJar)

license {
    header = project.file("HEADER.txt")
    include 'net/unknown/**/*.java'
    include 'org/spongepowered/asm/launch/UnknownNetworkBootstrap.java'
    include 'net/minecraft/launchwrapper/LaunchClassLoader.java'
    properties {
        organization = 'Unknown Network'
        year = 2023
    }
    newLine = true
}

javadoc.options.encoding = "UTF-8"
compileJava.options.encoding = "UTF-8"

java {
    withSourcesJar()
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "spongepowered"
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    paperweight.paperDevBundle("1.21.8-R0.1-SNAPSHOT")
    implementation "net.sf.jopt-simple:jopt-simple:5.0.4"
    implementation 'org.ow2.asm:asm:+'
    implementation 'org.ow2.asm:asm-commons:+'
    implementation 'org.ow2.asm:asm-util:+'
    implementation 'org.ow2.asm:asm-analysis:+'
    implementation "org.apache.logging.log4j:log4j-core:2.19.0"
    implementation "org.apache.logging.log4j:log4j-api:2.19.0"
    implementation ("net.fabricmc:sponge-mixin:+") {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'org.ow2.asm'
    }
}

jar {
    manifest {
        attributes('Premain-Class': 'net.unknown.launchwrapper.Agent')
        attributes('Agent-Class': 'net.unknown.launchwrapper.Agent')
        attributes('Launcher-Agent-Class': 'net.unknown.launchwrapper.Agent')
        attributes('Main-Class': 'net.unknown.launchwrapper.Main')
        attributes('MixinConfigs': 'unknownnetwork.mixins.json')
        attributes('Multi-Release': 'true')
    }
    archiveFileName = "original-${archiveBaseName.get()}.${archiveExtension.get()}"
}

sourcesJar {
    archiveFileName = "${archiveBaseName.get()}-sources.${archiveExtension.get()}"
}

shadowJar {
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

reobfJar {
    remapperArgs.addAll("{input}", "{output}", "{mappings}", "{from}", "{to}", "--threads=1", "--mixin") // Add Mixin Support
    outputJar = layout.buildDirectory.file("libs/${project.name}-spigot.jar")
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'net.unknown'
            artifactId = 'UnknownNetworkBootstrap'
            version = version
            artifact "build/libs/UnknownNetworkBootstrap.jar"

            artifact("build/libs/UnknownNetworkBootstrap-spigot.jar") {
                classifier "spigot"
            }

            pom {
                name = project.name
                description = "Unknown Network Bootstrapper"
                url = "https://www.unknownnet.jp"
                inceptionYear = "2022"

                licenses {
                    license {
                        name = "All rights reserved"
                    }
                }

                developers {
                    developer {
                        id = "ryuuta0217"
                        name = "Ryuta Iwakura"
                        email = "ryuuta.iwakura@gmail.com"
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url 'https://repo.ryuuta0217.com/repository/unknown-network/'
            credentials {
                username = System.getenv("NEXUS_USERNAME")
                password = System.getenv("NEXUS_PASSWORD")
            }
        }
    }

    publishReleasePublicationToMavenRepository {
        dependsOn shadowJar
        dependsOn reobfJar
    }
}